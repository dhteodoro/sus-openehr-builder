archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.problem_diagnosis.v1

concept
	[at0000]	-- Problem/Diagnosis
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Marco Borges">
				["organisation"] = <"P2D">
				["email"] = <"marco.borges@p2d.com.br">
			>
			accreditation = <"P2D Health Advisor Council">
		>
	>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"Ocean Informatics, UK">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2013-03-06">
	>
	details = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Para registrar detalhes sobre um caso ou obstáculo que tenha impactos sobre o bem estar físico, mental e/ou social de um indivíduo.">
			use = <"Usado para registrar qualquer informação sobre qualquer problema relacionado a saúde em geral.
O Arquétipo contém múltiplas datas, então pode registrar problemas presentes e/ou passados.
Um problema pode ser determinado tanto pelo próprio indivíduo como pelo profissional de saúde.
Alguns exemplos:
- Uma coleção de sintomas que estão sob investigação, mas pré-diagnose
- Desejo de perder peso, sem diagnóstico formal de Obesidade
- Desejo de parar de fumar pelo indivíduo
- Problemas de relacionamento com membro familiar">
			keywords = <"caso", "circunstância", "problema", "obstáculo">
			misuse = <"Não deve ser usado para registro de diagnósticos formais - usar a especialização deste arquétipo, EVALUATION.problem-diagnosis.">
			copyright = <"© openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording details about an issue or obstacle that impacts on the physical, mental and/or social well-being of an individual, including a formal medical diagnosis, nursing diagnosis or problem.">
			use = <"Use for recording any information about general health-related problems, diagnoses and nursing diagnoses.
The archetype contains multiple dates, so can record present and/or past problems. 
A problem can be determined by either the individual themselves or a healthcare provider. 
Some examples: 
- A range of symptoms that are under investigation but pre-diagnosis
- Desire to lose weight, without formal diagnosis of Obesity
- Desire to quit smoking by individual
- Relationship problem with family member.">
			keywords = <"issue", "condition", "problem", "obstacle", "diagnosis", "nursing diagnosis">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Koray Atalag, University of Auckland, New Zealand", "Steve Bentley, NHS CfH, United Kingdom", "Ian Bull, ACT Health, Australia", "Sergio Carmona, Chile", "Rong Chen, Cambio Healthcare Systems, Sweden", "Stephen Chu, NeHTA, Australia", "Ed Conley, Cardiff University", "Paul Donaldson, Nursing Informatics Australia, Australia", "Anneke Goossen, Results 4 Care, Netherlands", "Heather Grain, Llewelyn Grain Informatics, Australia", "Sam Heard, Ocean Informatics, Australia", "Evelyn Hovenga, EJSH Consulting, Australia", "Eugene Igras, IRIS Systems, Inc., Canada", "Eizen Kimura, Ehime Univ., Japan", "Shinji Kobayashi, Ehime University, Japan", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Rohan Martin, Ambulance Victoria, Australia", "Ian McNicoll, Ocean Informatics, United Kingdom (Editor)", "Jörg Niggemann, compugroup, Germany", "Andrej Orel, Marand d.o.o., Slovenia">
	other_details = <
		["MD5-CAM-1.0.1"] = <"2CD426CD82409C217348260593A062A2">
	>

definition
	EVALUATION[at0000] matches {	-- Problem/Diagnosis
		data matches {
			ITEM_TREE[at0001] matches {	-- structure
				items cardinality matches {1..*; ordered} matches {
					ELEMENT[at0002] matches {	-- Problem/Diagnosis
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Severity
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {[local::]}
							}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Date of Onset
						value matches {
							DV_DATE_TIME matches {
								value matches {yyyy-mm-ddTHH:MM:SS}
							}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Age at Onset
						value matches {
							DV_DURATION matches {*}
						}
					}
					ELEMENT[at0012] occurrences matches {0..1} matches {	-- Body Site
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0039] occurrences matches {0..1} matches {	-- Body Site Details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1/}
					}
					CLUSTER[at0018] occurrences matches {0..1} matches {	-- Previous Occurrences
						items cardinality matches {1..*; ordered} matches {
							ELEMENT[at0019] occurrences matches {0..1} matches {	-- Frequency
								value matches {
									C_DV_QUANTITY <
										property = <[openehr::382]>
										list = <
											["1"] = <
												units = <"/min">
											>
											["2"] = <
												units = <"/h">
												magnitude = <|>=0.0|>
											>
											["3"] = <
												units = <"/d">
												magnitude = <|>=0.0|>
											>
											["4"] = <
												units = <"/wk">
												magnitude = <|>=0.0|>
											>
											["5"] = <
												units = <"/mo">
												magnitude = <|>=0.0|>
											>
											["6"] = <
												units = <"/yr">
												magnitude = <|>=0.0|>
											>
										>
									>
								}
							}
							ELEMENT[at0020] occurrences matches {0..1} matches {	-- Date of last Occurrence
								value matches {
									DV_DATE_TIME matches {
										value matches {yyyy-mm-ddTHH:MM:SS}
									}
								}
							}
							ELEMENT[at0025] occurrences matches {0..1} matches {	-- Number
								value matches {
									DV_COUNT matches {
										magnitude matches {|>=1|}
									}
								}
							}
							ELEMENT[at0022] occurrences matches {0..1} matches {	-- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					CLUSTER[at0027] occurrences matches {0..*} matches {	-- Related Item
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0029] occurrences matches {0..1} matches {	-- Relationship Type
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0040, 	-- Caused by
											at0041]	-- Following
										}
									}
								}
							}
							ELEMENT[at0028] occurrences matches {0..1} matches {	-- Item
								value matches {
									DV_TEXT matches {*}
									DV_URI matches {*}
								}
							}
							ELEMENT[at0044] occurrences matches {0..1} matches {	-- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					ELEMENT[at0030] occurrences matches {0..1} matches {	-- Date of Resolution
						value matches {
							DV_DATE matches {*}
						}
					}
					ELEMENT[at0031] occurrences matches {0..1} matches {	-- Age at Resolution
						value matches {
							DV_DURATION matches {*}
						}
					}
					allow_archetype CLUSTER[at0042] occurrences matches {0..1} matches {	-- Diagnostic Criteria
						include
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0043] occurrences matches {0..*} matches {	-- Clinical Stage/Grade
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0032] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0035] occurrences matches {0..*} matches {	-- Supporting clinical evidence
						value matches {
							DV_EHR_URI matches {*}
						}
					}
					allow_archetype CLUSTER[at0046] occurrences matches {0..*} matches {	-- Status
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Problem/Diagnosis">
					description = <"An issue or obstacle which adversely impacts on the physical, mental and/or social well-being of an individual. The definition of a problem is deliberately kept rather loose and inclusive of a formal biomedical diagnosis so as to capture any real or perceived concerns that may adversely affect an individual's wellbeing to any degree.">
				>
				["at0001"] = <
					text = <"structure">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Problem/Diagnosis">
					description = <"Identification of the index problem, issue or diagnosis.">
				>
				["at0003"] = <
					text = <"Date of Onset">
					description = <"The date / time when the problem was first identified by the individual.">
				>
				["at0004"] = <
					text = <"Age at Onset">
					description = <"The age of the individual when the problem was first identified by the individual.">
				>
				["at0005"] = <
					text = <"Severity">
					description = <"The assessed overall severity of the index problem.">
					comment = <" Coding of the severity with a terminology is preferred, where possible.">
				>
				["at0009"] = <
					text = <"Description">
					description = <"A narrative description of the  issue, problem or diagnosis.">
				>
				["at0012"] = <
					text = <"Body Site">
					description = <"The body site affected.">
				>
				["at0018"] = <
					text = <"Previous Occurrences">
					description = <"Grouping of information about individual occurrences or exacerbations.">
				>
				["at0019"] = <
					text = <"Frequency">
					description = <"The frequency of individual occurrences of the problem.">
				>
				["at0020"] = <
					text = <"Date of last Occurrence">
					description = <"The date / time of the last occurrence of the problem.">
				>
				["at0022"] = <
					text = <"Description">
					description = <"A description of the exacerbation or occurrence.">
				>
				["at0025"] = <
					text = <"Number">
					description = <"Number of times this problem has occurred or been apparent.">
				>
				["at0027"] = <
					text = <"Related Item">
					description = <"A group of characteristics of the problem complicating the index problem.">
				>
				["at0028"] = <
					text = <"Item">
					description = <"Identification of a related problem as text,coded text or link within the EHR.">
				>
				["at0029"] = <
					text = <"Relationship Type">
					description = <"The type of relationship that this problem/diagnosis has to the related item.">
				>
				["at0030"] = <
					text = <"Date of Resolution">
					description = <"The date that the problem resolved or went into remission.">
				>
				["at0031"] = <
					text = <"Age at Resolution">
					description = <"The age of the person at the resolution of the problem.">
				>
				["at0032"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0035"] = <
					text = <"Supporting clinical evidence">
					description = <"Useful information on the internet about this condition.">
				>
				["at0039"] = <
					text = <"Body Site Details">
					description = <"Details of the body site concerned.">
				>
				["at0040"] = <
					text = <"Caused by">
					description = <"This concept identifies the direct cause or causative agent of a Problem/Diagnosis. The concept includes the idea of ‘complications’, ‘causative agent’ and ‘due to’. Note: Where no causality or sequence of events is known, this relationship type should be left blank.">
				>
				["at0041"] = <
					text = <"Following">
					description = <"This value identifies the sequence of events between the related items, but does not assert causality. This can be used for sequelae or late effects. Note: Where no causality or sequence of events is known, this relationship type should be left blank.">
				>
				["at0042"] = <
					text = <"Diagnostic Criteria">
					description = <"The criteria on which the problem/diagnosis is based.">
				>
				["at0043"] = <
					text = <"Clinical Stage/Grade">
					description = <"Clinical stage or grade of a problem/diagnosis. Note: May include formal severity assessments.">
				>
				["at0044"] = <
					text = <"Description">
					description = <"A narrative description of the related item.">
				>
				["at0046"] = <
					text = <"Status">
					description = <"Descriptor for context- or use-case specific label or workflow-related aspect of the diagnostic process which may not be safe to exchange between systems or use in a shared environment. Examples include: active/inactive; primary/secondary; preliminary/provisional/working/final.">
				>
			>
		>
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Problema">
					description = <"Um caso ou obstáculo que tenha impactos sobre o bem estar físico, mental e/ou social de um indivíduo">
				>
				["at0001"] = <
					text = <"structure">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Problema">
					description = <"Identificação dos indicadores do problema">
				>
				["at0003"] = <
					text = <"Data de começo inicial">
					description = <"A data de quando o problema foi identificado pela primeira vez pelo indivíduo.">
				>
				["at0004"] = <
					text = <"A idade de começo inicial">
					description = <"A idade do indivíduo quando o problema foi identificado pela primeira vez pelo indivíduo.">
				>
				["at0005"] = <
					text = <"Gravidade">
					description = <"A avaliação global da gravidade dos indicadores problema.">
				>
				["at0009"] = <
					text = <"Descrição Clinica">
					description = <"Descrição dos aspectos clínicos do problema.">
				>
				["at0012"] = <
					text = <"Local do corpo">
					description = <"O local do corpo afetado.">
				>
				["at0018"] = <
					text = <"Ocorrências ou exacerbações">
					description = <"Agrupamento de informações sobre ocorrências individuais ou exacerbações.">
				>
				["at0019"] = <
					text = <"Frequência de recorrência">
					description = <"A frequência de ocorrências individuais do problema.">
				>
				["at0020"] = <
					text = <"Data da última ocorrência">
					description = <"A data da última ocorrência do problema.">
				>
				["at0022"] = <
					text = <"Descrição clínica">
					description = <"Uma descrição da exacerbação ou ocorrência.">
				>
				["at0025"] = <
					text = <"Número de ocorrências">
					description = <"Número de vezes que este problema ocorreu ou foi aparente.">
				>
				["at0027"] = <
					text = <"Problema relacionado">
					description = <"Um grupo de características do problema complicando os indicadores do problema.">
				>
				["at0028"] = <
					text = <"Problema relacionado">
					description = <"dentificação textual de um problema relacionado, texto codificado ou link dentro do RES.">
				>
				["at0029"] = <
					text = <"Descrição clínica">
					description = <"Decription do problema relacionado.">
				>
				["at0030"] = <
					text = <"Data da resolução">
					description = <"A data em que o problema resolvido ou entrou em remissão.">
				>
				["at0031"] = <
					text = <"Idade na resolução">
					description = <"A idade da pessoa com a resolução do problema.">
				>
				["at0032"] = <
					text = <"Tree(en)">
					description = <"@ internal @">
				>
				["at0035"] = <
					text = <"Web link">
					description = <"Informações úteis na internet sobre essa condição.">
				>
				["at0039"] = <
					text = <"*Cluster(en)">
					description = <"**(en)">
				>
				["at0040"] = <
					text = <"*Caused by(en)">
					description = <"*This concept identifies the direct cause or causative agent of a Problem/Diagnosis. The concept includes the idea of ‘complications’, ‘causative agent’ and ‘due to’. Note: Where no causality or sequence of events is known, this relationship type should be left blank.(en)">
				>
				["at0041"] = <
					text = <"*Following(en)">
					description = <"*his value identifies the sequence of events between the related items, but does not assert causality. This can be used for sequelae or late effects. Note: Where no causality or sequence of events is known, this relationship type should be left blank.(en)">
				>
				["at0042"] = <
					text = <"*Cluster(en)">
					description = <"**(en)">
				>
				["at0043"] = <
					text = <"*Cluster(en)">
					description = <"**(en)">
				>
				["at0044"] = <
					text = <"*New element(en)">
					description = <"**(en)">
				>
				["at0046"] = <
					text = <"*Cluster(en)">
					description = <"**(en)">
				>
			>
		>
	>
